! (C) Copyright 1988- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE VALIDATE_ATLAS_MOD
  USE PARKIND1, ONLY: JPIM, JPRB
  USE CLOUDSC_MPI_MOD
  USE VALIDATE_MOD, ONLY: VALIDATE, ERROR_PRINT

  USE ATLAS_MODULE
  USE ATLAS_FIELDSET_MODULE
  USE ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS_MODULE
  USE, INTRINSIC :: ISO_C_BINDING
  USE EXPAND_MOD, ONLY: LOAD_AND_EXPAND

  IMPLICIT NONE

CONTAINS

  SUBROUTINE VALIDATESTATE_ATLAS(FSET, FSPACE, NAME, NLON, NGPTOTG)
    TYPE(ATLAS_FIELDSET), INTENT(INOUT) :: FSET
    TYPE(ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS), INTENT(IN) :: FSPACE
    CHARACTER(*), INTENT(IN) :: NAME
    INTEGER(KIND=JPIM), INTENT(IN) :: NLON
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NGPTOTG

    CALL VALIDATEVAR_ATLAS(FSET, FSPACE, NAME, NLON, NGPTOTG, "A")
    CALL VALIDATEVAR_ATLAS(FSET, FSPACE, NAME, NLON, NGPTOTG, "Q")
    CALL VALIDATEVAR_ATLAS(FSET, FSPACE, NAME, NLON, NGPTOTG, "T")
    CALL VALIDATEVAR_ATLAS(FSET, FSPACE, NAME, NLON, NGPTOTG, "CLD")
  END SUBROUTINE VALIDATESTATE_ATLAS

  SUBROUTINE VALIDATEVAR_ATLAS(FSET, FSPACE, NAME, NLON, NGPTOTG, STATE_VAR)
    ! Computes and prints errors "in the L1 norm sense"
    TYPE(ATLAS_FIELDSET), INTENT(INOUT) :: FSET
    TYPE(ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS), INTENT(IN) :: FSPACE
    CHARACTER(*), INTENT(IN) :: NAME
    INTEGER(KIND=JPIM), INTENT(IN) :: NLON
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NGPTOTG
    CHARACTER(*), INTENT(IN), OPTIONAL :: STATE_VAR

    REAL(KIND=JPRB), ALLOCATABLE :: REF_R2(:,:), REF_R3(:,:,:), REF_R4(:,:,:,:)
    REAL(KIND=JPRB), POINTER :: FIELD_R1(:,:), FIELD_R2(:,:,:), FIELD_R3(:,:,:,:)
    TYPE(ATLAS_FIELD) :: FIELD
    INTEGER :: B, BSIZE, JL, JK, JM
    REAL(KIND=JPRB) :: ZMINVAL(1), ZMAX_VAL_ERR(2), ZDIFF, ZSUM_ERR_ABS(2), ZRELERR, ZAVGPGP
    INTEGER :: FRANK, NBLOCKS, NLEV, NGPTOT, VAR_ID, NDIM, NPROMA
    CHARACTER(LEN=256) :: FULLNAME

    IF (PRESENT(STATE_VAR)) THEN
      FULLNAME = NAME//'_'//STATE_VAR
    ELSE
      FULLNAME = NAME
    ENDIF

    FIELD = FSET%FIELD(NAME)
    FRANK = FIELD%RANK()
    NLEV = FIELD%LEVELS()
    NGPTOT = FSPACE%SIZE()
    NBLOCKS = FSPACE%NBLKS()
    NPROMA = FIELD%SHAPE(1)

    ZMINVAL(1) = +HUGE(ZMINVAL(1))
    ZMAX_VAL_ERR(1) = -HUGE(ZMAX_VAL_ERR(1))
    ZMAX_VAL_ERR(2) = 0.0_JPRB
    ZSUM_ERR_ABS(:) = 0.0_JPRB

    IF (FRANK == 2) THEN
        CALL LOAD_AND_EXPAND(NAME, REF_R2, NLON, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
        CALL FIELD%DATA(FIELD_R1)
        !OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(B, BSIZE) &
        !& REDUCTION(MIN:ZMINVAL, MAX:ZMAX_VAL_ERR, +:ZSUM_ERR_ABS)
        DO B=1, NBLOCKS
          BSIZE = FSPACE%BLOCK_SIZE(B)
          ZMINVAL(1) = MIN(ZMINVAL(1),MINVAL(FIELD_R1(:,B)))
          ZMAX_VAL_ERR(1) = MAX(ZMAX_VAL_ERR(1),MAXVAL(FIELD_R1(:,B)))
          DO JK=1, BSIZE
            ! Difference against reference result in one-norm sense
            ZDIFF = ABS(FIELD_R1(JK,B) - REF_R2(JK,B))
            ZMAX_VAL_ERR(2) = MAX(ZMAX_VAL_ERR(2),ZDIFF)
            ZSUM_ERR_ABS(1) = ZSUM_ERR_ABS(1) + ZDIFF
            ZSUM_ERR_ABS(2) = ZSUM_ERR_ABS(2) + ABS(REF_R2(JK,B))
          ENDDO
        END DO
    ELSE IF (FRANK == 3) THEN
        CALL LOAD_AND_EXPAND(NAME, REF_R3, NLON, FIELD%LEVELS(), NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
        CALL FIELD%DATA(FIELD_R2)
        !OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(B, BSIZE) &
        !& REDUCTION(MIN:ZMINVAL, MAX:ZMAX_VAL_ERR, +:ZSUM_ERR_ABS)
        DO B=1, NBLOCKS
          BSIZE = FSPACE%BLOCK_SIZE(B)
          ZMINVAL(1) = MIN(ZMINVAL(1),MINVAL(FIELD_R2(:,:,B)))
          ZMAX_VAL_ERR(1) = MAX(ZMAX_VAL_ERR(1),MAXVAL(FIELD_R2(:,:,B)))
          DO JL=1, NLEV
            DO JK=1, BSIZE
              ! Difference against reference result in one-norm sense
              ZDIFF = ABS(FIELD_R2(JK,JL,B) - REF_R3(JK,JL,B))
              ZMAX_VAL_ERR(2) = MAX(ZMAX_VAL_ERR(2),ZDIFF)
              ZSUM_ERR_ABS(1) = ZSUM_ERR_ABS(1) + ZDIFF
              ZSUM_ERR_ABS(2) = ZSUM_ERR_ABS(2) + ABS(REF_R3(JK,JL,B))
            ENDDO
          END DO
        END DO
      ELSE IF (FRANK == 4 .AND. PRESENT(STATE_VAR)) THEN
        CALL FIELD%DATA(FIELD_R3)
        NDIM = FIELD%SHAPE(3) - 3
        IF (STATE_VAR /= 'CLD') THEN
            VAR_ID = 1
            IF (STATE_VAR == 'A') THEN
                VAR_ID = 2
            ENDIF
            IF (STATE_VAR == 'Q') THEN
                VAR_ID = 3
            ENDIF
            CALL LOAD_AND_EXPAND(NAME//'_'//STATE_VAR, REF_R3, NLON, NLEV, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
            !OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(B, BSIZE) &
            !& REDUCTION(MIN:ZMINVAL, MAX:ZMAX_VAL_ERR, +:ZSUM_ERR_ABS)
            DO B=1, NBLOCKS
              BSIZE = FSPACE%BLOCK_SIZE(B)
              ZMINVAL(1) = MIN(ZMINVAL(1),MINVAL(FIELD_R3(:,:,VAR_ID,B)))
              ZMAX_VAL_ERR(1) = MAX(ZMAX_VAL_ERR(1),MAXVAL(FIELD_R3(:,:,VAR_ID,B)))
              DO JL=1, NLEV
                DO JK=1, BSIZE
                  ! Difference against reference result in one-norm sense
                  ZDIFF = ABS(FIELD_R3(JK,JL,VAR_ID,B) - REF_R3(JK,JL,B))
                  ZMAX_VAL_ERR(2) = MAX(ZMAX_VAL_ERR(2),ZDIFF)
                  ZSUM_ERR_ABS(1) = ZSUM_ERR_ABS(1) + ZDIFF
                  ZSUM_ERR_ABS(2) = ZSUM_ERR_ABS(2) + ABS(REF_R3(JK,JL,B))
                ENDDO
              END DO
            END DO
        ELSE IF (STATE_VAR == 'CLD') THEN
          CALL LOAD_AND_EXPAND(NAME//'_CLD', REF_R4, NLON, NLEV, NDIM, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
          !OMP PARALLEL DO DEFAULT(SHARED) PRIVATE(B, BSIZE) &
          !& REDUCTION(MIN:ZMINVAL, MAX:ZMAX_VAL_ERR, +:ZSUM_ERR_ABS)
          DO B=1, NBLOCKS
            BSIZE = FSPACE%BLOCK_SIZE(B)
            ZMINVAL(1) = MIN(ZMINVAL(1),MINVAL(FIELD_R3(:,:,4:,B)))
            ZMAX_VAL_ERR(1) = MAX(ZMAX_VAL_ERR(1),MAXVAL(FIELD_R3(:,:,4:,B)))
            DO JM=1, NDIM
              DO JL=1, NLEV
                DO JK=1, BSIZE
                  ! Difference against reference result in one-norm sense
                  ZDIFF = ABS(FIELD_R3(JK,JL,3+JM,B) - REF_R4(JK,JL,JM,B))
                  ZMAX_VAL_ERR(2) = MAX(ZMAX_VAL_ERR(2),ZDIFF)
                  ZSUM_ERR_ABS(1) = ZSUM_ERR_ABS(1) + ZDIFF
                  ZSUM_ERR_ABS(2) = ZSUM_ERR_ABS(2) + ABS(REF_R4(JK,JL,JM,B))
                ENDDO
              ENDDO
            END DO
          END DO
        ENDIF
      ELSE
        PRINT *, "FIELD RANK NOT SUPPORTED"
        CALL EXIT(1)
    ENDIF

    CALL CLOUDSC_MPI_REDUCE_MIN(ZMINVAL, 1, 0)
    CALL CLOUDSC_MPI_REDUCE_MAX(ZMAX_VAL_ERR, 2, 0)
    CALL CLOUDSC_MPI_REDUCE_SUM(ZSUM_ERR_ABS, 2, 0)

    IF (PRESENT(NGPTOTG)) THEN
      ZAVGPGP = ZSUM_ERR_ABS(1) / REAL(NGPTOTG,JPRB)
    ELSE
      ZAVGPGP = ZSUM_ERR_ABS(1) / REAL(NGPTOT,JPRB)
    END IF

    IF (IRANK == 0) THEN
      CALL ERROR_PRINT(FULLNAME, ZMINVAL(1), ZMAX_VAL_ERR(1), ZMAX_VAL_ERR(2), &
        & ZSUM_ERR_ABS(1), ZSUM_ERR_ABS(2), ZAVGPGP, NDIM=FRANK-1)
    END IF

    CALL FIELD%FINAL()
  END SUBROUTINE VALIDATEVAR_ATLAS

END MODULE VALIDATE_ATLAS_MOD
