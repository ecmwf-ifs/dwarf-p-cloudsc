! (C) Copyright 1988- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE CLOUDSC_STATE_TYPE_MOD
  ! Driver module to manage the setup and teardown of the field-based state
  USE PARKIND1,  ONLY : JPIM, JPRB
  USE YOECLDP,   ONLY : NCLV

  USE FIELD_MODULE, ONLY: FIELD_2RB, FIELD_3RB, FIELD_4RB, FIELD_2IM, FIELD_2LM, FIELD_3RB_PTR
  USE FIELD_FACTORY_MODULE

  IMPLICIT NONE


    TYPE CLOUDSC_STATE_TYPE

      LOGICAL :: PACKED
      REAL(KIND=JPRB), DIMENSION(:,:), POINTER :: T   ! GMV FIELDS
      REAL(KIND=JPRB), DIMENSION(:,:), POINTER :: Q, A  ! GFL FIELDS
      REAL(KIND=JPRB), DIMENSION(:,:,:), POINTER :: CLD   ! COMPOSED CLOUD ARRAY
 
      CLASS(FIELD_3RB), POINTER :: F_T, F_A, F_Q
      CLASS(FIELD_4RB), POINTER :: F_CLD

      CLASS(FIELD_4RB), POINTER :: FIELD_GANG
      TYPE(FIELD_3RB_PTR), PRIVATE, ALLOCATABLE :: FIELD_PTRS(:)

    CONTAINS 
      PROCEDURE :: INIT => STATE_TYPE_INIT
      PROCEDURE :: UPDATE_VIEW => STATE_TYPE_UPDATE_VIEW
      PROCEDURE :: SYNC_HOST => STATE_TYPE_SYNC_HOST
      PROCEDURE :: FINAL => STATE_TYPE_FINAL

    END TYPE CLOUDSC_STATE_TYPE

CONTAINS

  SUBROUTINE STATE_TYPE_INIT(SELF,NPROMA, NGPTOT, KLON, KLEV, KFLDX, NBLOCKS, NGPTOTG, USE_PACKED)
    CLASS(CLOUDSC_STATE_TYPE) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NGPTOT, KLON, KLEV, KFLDX, NBLOCKS 
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NGPTOTG
    LOGICAL, INTENT(IN), OPTIONAL :: USE_PACKED
    
    INTEGER(KIND=JPIM), PARAMETER :: NPACKED_FIELDS = 3

    SELF%PACKED = .FALSE.
    IF (PRESENT(USE_PACKED)) SELF%PACKED = USE_PACKED
     
    IF (SELF%PACKED) THEN
      CALL FIELD_NEW(SELF%FIELD_GANG, SELF%FIELD_PTRS, UBOUNDS=[NPROMA,KLEV,NPACKED_FIELDS,NBLOCKS], &
        & PERSISTENT=.TRUE., INIT_VALUE=0._JPRB)
        SELF%F_T => SELF%FIELD_PTRS(1)%PTR
        SELF%F_A => SELF%FIELD_PTRS(2)%PTR
        SELF%F_Q => SELF%FIELD_PTRS(3)%PTR
    ELSE
      CALL FIELD_NEW(SELF%F_T, UBOUNDS=[NPROMA,KLEV,NBLOCKS], PERSISTENT=.TRUE., INIT_VALUE=0._JPRB)
      CALL FIELD_NEW(SELF%F_A, UBOUNDS=[NPROMA,KLEV,NBLOCKS], PERSISTENT=.TRUE., INIT_VALUE=0._JPRB)
      CALL FIELD_NEW(SELF%F_Q, UBOUNDS=[NPROMA,KLEV,NBLOCKS], PERSISTENT=.TRUE., INIT_VALUE=0._JPRB)
    END IF
    CALL FIELD_NEW(SELF%F_CLD, UBOUNDS=[NPROMA,KLEV,NCLV,NBLOCKS], PERSISTENT=.TRUE., INIT_VALUE=0._JPRB)
    
  END SUBROUTINE STATE_TYPE_INIT
  
  
  SUBROUTINE STATE_TYPE_UPDATE_VIEW(SELF, BLOCK_INDEX)
    CLASS(CLOUDSC_STATE_TYPE) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: BLOCK_INDEX
    
    IF(ASSOCIATED(SELF%F_T)) SELF%T => SELF%F_T%GET_VIEW(BLOCK_INDEX)
    IF(ASSOCIATED(SELF%F_Q)) SELF%Q => SELF%F_Q%GET_VIEW(BLOCK_INDEX)
    IF(ASSOCIATED(SELF%F_A)) SELF%A => SELF%F_A%GET_VIEW(BLOCK_INDEX)
    IF(ASSOCIATED(SELF%F_CLD)) SELF%CLD => SELF%F_CLD%GET_VIEW(BLOCK_INDEX)
     
  END SUBROUTINE STATE_TYPE_UPDATE_VIEW


  SUBROUTINE STATE_TYPE_SYNC_HOST(SELF)
    CLASS(CLOUDSC_STATE_TYPE) :: SELF
    CALL SELF%F_T%SYNC_HOST_RDWR()
    CALL SELF%F_Q%SYNC_HOST_RDWR()
    CALL SELF%F_A%SYNC_HOST_RDWR()
    CALL SELF%F_CLD%SYNC_HOST_RDWR()
    ! Note , this deletion is done in the IFS, I am not sure, whether we wan't to do this in the cloudsc kernel
    IF (ASSOCIATED(SELF%F_T))   CALL SELF%F_T%DELETE_DEVICE_DATA()
    IF (ASSOCIATED(SELF%F_Q))   CALL SELF%F_Q%DELETE_DEVICE_DATA()
    IF (ASSOCIATED(SELF%F_A))   CALL SELF%F_A%DELETE_DEVICE_DATA()
    IF (ASSOCIATED(SELF%F_CLD)) CALL SELF%F_CLD%DELETE_DEVICE_DATA()
  END SUBROUTINE STATE_TYPE_SYNC_HOST
  
  SUBROUTINE STATE_TYPE_FINAL(SELF)
    CLASS(CLOUDSC_STATE_TYPE) :: SELF
    IF (SELF%PACKED) THEN
     CALL FIELD_DELETE(SELF%FIELD_GANG)
     DEALLOCATE(SELF%FIELD_GANG)
    ELSE
      CALL FIELD_DELETE(SELF%F_T)
      CALL FIELD_DELETE(SELF%F_Q)
      CALL FIELD_DELETE(SELF%F_A)
    END IF
    CALL FIELD_DELETE(SELF%F_CLD)
  END SUBROUTINE STATE_TYPE_FINAL

END MODULE CLOUDSC_STATE_TYPE_MOD

