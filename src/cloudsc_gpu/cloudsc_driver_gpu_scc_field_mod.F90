! (C) Copyright 1988- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE CLOUDSC_DRIVER_GPU_SCC_FIELD_MOD

  USE PARKIND1, ONLY: JPIM, JPRB
  USE YOMPHYDER, ONLY: STATE_TYPE
  USE YOECLDP, ONLY : NCLV, YRECLDP, TECLDP
  USE CLOUDSC_MPI_MOD, ONLY: NUMPROC, IRANK
  USE TIMER_MOD, ONLY : PERFORMANCE_TIMER, GET_THREAD_NUM

  USE CLOUDSC_GPU_SCC_MOD, ONLY: CLOUDSC_SCC
  USE CLOUDSC_FIELD_STATE_MOD, ONLY: CLOUDSC_FIELD_STATE
  USE FIELD_MODULE, ONLY: GET_DEVICE_DATA, ENSURE_HOST

  IMPLICIT NONE

CONTAINS

  SUBROUTINE CLOUDSC_DRIVER_GPU_SCC_FIELD( &
     & NUMOMP, NPROMA, NLEV, NGPTOT, NGPBLKS, NGPTOTG, KFLDX, PTSPHY, FIELD_STATE, USE_PACKED &
     & )
    ! Driver routine that invokes the optimized CLAW-based CLOUDSC GPU kernel

    INTEGER(KIND=JPIM)                                    :: NUMOMP, NPROMA, NLEV, NGPTOT, NGPBLKS, NGPTOTG
    INTEGER(KIND=JPIM)                                    :: KFLDX 
    REAL(KIND=JPRB)                                       :: PTSPHY       ! Physics timestep
    TYPE(CLOUDSC_FIELD_STATE), INTENT(INOUT)              :: FIELD_STATE
    LOGICAL, INTENT(IN) :: USE_PACKED

    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PT(:,:,:)       ! T at start of callpar
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PQ(:,:,:)       ! Q at start of callpar
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PVFA(:,:,:)     ! CC from VDF scheme
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PVFL(:,:,:)     ! Liq from VDF scheme
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PVFI(:,:,:)     ! Ice from VDF scheme
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PDYNA(:,:,:)    ! CC from Dynamics
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PDYNL(:,:,:)    ! Liq from Dynamics
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PDYNI(:,:,:)    ! Liq from Dynamics
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PHRSW(:,:,:)    ! Short-wave heating rate
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PHRLW(:,:,:)    ! Long-wave heating rate
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PVERVEL(:,:,:)  ! Vertical velocity
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PAP(:,:,:)      ! Pressure on full levels
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PAPH(:,:,:)     ! Pressure on half levels
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLSM(:,:)       ! Land fraction (0-1)
    LOGICAL, POINTER, CONTIGUOUS          :: LDCUM(:,:)      ! Convection active
    INTEGER(KIND=JPIM),POINTER, CONTIGUOUS:: KTYPE(:,:)      ! Convection type 0,1,2
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLU(:,:,:)      ! Conv. condensate
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLUDE(:,:,:)    ! Conv. detrained water
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PSNDE(:,:,:)    ! Conv. detrained snow
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PMFU(:,:,:)     ! Conv. mass flux up
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PMFD(:,:,:)     ! Conv. mass flux down
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PA(:,:,:)       ! Original Cloud fraction (t)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PCLV(:,:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PSUPSAT(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLCRIT_AER(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PICRIT_AER(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PRE_ICE(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PCCN(:,:,:)     ! liquid cloud condensation nuclei
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PNICE(:,:,:)    ! ice number concentration (cf. CCN)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PCOVPTOT(:,:,:)    ! Precip fraction
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PRAINFRAC_TOPRFZ(:,:)
    ! Flux diagnostics for DDH budget
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQLF(:,:,:)    ! Flux of liquid
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQIF(:,:,:)    ! Flux of ice
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFCQLNG(:,:,:)   ! -ve corr for liq
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFCQNNG(:,:,:)   ! -ve corr for ice
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQRF(:,:,:)    ! Flux diagnostics
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQSF(:,:,:)    !    for DDH, generic
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFCQRNG(:,:,:)   ! rain
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFCQSNG(:,:,:)   ! snow
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQLTUR(:,:,:)  ! liquid flux due to VDF
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQITUR(:,:,:)  ! ice flux due to VDF
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFPLSL(:,:,:)    ! liq+rain sedim flux
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFPLSN(:,:,:)    ! ice+snow sedim flux
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFHPSL(:,:,:)    ! Enthalpy flux for liq
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFHPSN(:,:,:)    ! ice number concentration (cf. CCN)

    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_T(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_Q(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_A(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_CLD(:,:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_T(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_Q(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_A(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_CLD(:,:,:,:)

    INTEGER(KIND=JPIM) :: JKGLO,IBL,ICEND
    TYPE(PERFORMANCE_TIMER) :: TIMER
    INTEGER(KIND=JPIM) :: TID ! thread id from 0 .. NUMOMP - 1

    NGPBLKS = (NGPTOT / NPROMA) + MIN(MOD(NGPTOT,NPROMA), 1)
1003 format(5x,'NUMPROC=',i0,', NUMOMP=',i0,', NGPTOTG=',i0,', NPROMA=',i0,', NGPBLKS=',i0)
    if (irank == 0) then
      write(0,1003) NUMPROC,NUMOMP,NGPTOTG,NPROMA,NGPBLKS
    end if

    ! Global timer for the parallel region
    CALL TIMER%START(NUMOMP)

    IF(USE_PACKED)THEN
       CALL FIELD_STATE%DATA_RDONLY%SYNC_DEVICE_RDONLY()
       CALL FIELD_STATE%DATA_RWONLY%SYNC_DEVICE_RDWR()
       ! If this is called then the subsequent FIELD_STATE%FIELDS_RDONLY/RWONLY%PTR%GET_DEVICE_DATA()
       ! calls don't trigger any data movement, they just return an updated device pointer
    ENDIF

    CALL FIELD_STATE%FIELDS_RDONLY(1)%PTR%GET_DEVICE_DATA_RDONLY(PT)
    CALL FIELD_STATE%FIELDS_RDONLY(2)%PTR%GET_DEVICE_DATA_RDONLY(PQ)
    CALL FIELD_STATE%FIELDS_RDONLY(3)%PTR%GET_DEVICE_DATA_RDONLY(PVFA)
    CALL FIELD_STATE%FIELDS_RDONLY(4)%PTR%GET_DEVICE_DATA_RDONLY(PVFL)
    CALL FIELD_STATE%FIELDS_RDONLY(5)%PTR%GET_DEVICE_DATA_RDONLY(PVFI)
    CALL FIELD_STATE%FIELDS_RDONLY(6)%PTR%GET_DEVICE_DATA_RDONLY(PDYNA)
    CALL FIELD_STATE%FIELDS_RDONLY(7)%PTR%GET_DEVICE_DATA_RDONLY(PDYNL)
    CALL FIELD_STATE%FIELDS_RDONLY(8)%PTR%GET_DEVICE_DATA_RDONLY(PDYNI)
    CALL FIELD_STATE%FIELDS_RDONLY(9)%PTR%GET_DEVICE_DATA_RDONLY(PHRSW)
    CALL FIELD_STATE%FIELDS_RDONLY(10)%PTR%GET_DEVICE_DATA_RDONLY(PHRLW)
    CALL FIELD_STATE%FIELDS_RDONLY(11)%PTR%GET_DEVICE_DATA_RDONLY(PVERVEL)
    CALL FIELD_STATE%FIELDS_RDONLY(12)%PTR%GET_DEVICE_DATA_RDONLY(PAP)
    CALL FIELD_STATE%F_PAPH%GET_DEVICE_DATA_RDONLY(PAPH)
    CALL FIELD_STATE%F_PLSM%GET_DEVICE_DATA_RDONLY(PLSM)
    CALL FIELD_STATE%F_LDCUM%GET_DEVICE_DATA_RDONLY(LDCUM)
    CALL FIELD_STATE%F_KTYPE%GET_DEVICE_DATA_RDONLY(KTYPE)
    CALL FIELD_STATE%FIELDS_RDONLY(13)%PTR%GET_DEVICE_DATA_RDONLY(PLU)
    CALL FIELD_STATE%FIELDS_RDONLY(14)%PTR%GET_DEVICE_DATA_RDONLY(PSNDE)
    CALL FIELD_STATE%FIELDS_RDONLY(15)%PTR%GET_DEVICE_DATA_RDONLY(PMFU)
    CALL FIELD_STATE%FIELDS_RDONLY(16)%PTR%GET_DEVICE_DATA_RDONLY(PMFD)
    CALL FIELD_STATE%FIELDS_RDONLY(17)%PTR%GET_DEVICE_DATA_RDONLY(PA)
    CALL FIELD_STATE%F_PCLV%GET_DEVICE_DATA_RDONLY(PCLV)
    CALL FIELD_STATE%FIELDS_RDONLY(18)%PTR%GET_DEVICE_DATA_RDONLY(PSUPSAT)
    CALL FIELD_STATE%FIELDS_RDONLY(19)%PTR%GET_DEVICE_DATA_RDONLY(PLCRIT_AER)
    CALL FIELD_STATE%FIELDS_RDONLY(20)%PTR%GET_DEVICE_DATA_RDONLY(PICRIT_AER)
    CALL FIELD_STATE%FIELDS_RDONLY(21)%PTR%GET_DEVICE_DATA_RDONLY(PRE_ICE)
    CALL FIELD_STATE%FIELDS_RDONLY(22)%PTR%GET_DEVICE_DATA_RDONLY(PCCN)
    CALL FIELD_STATE%FIELDS_RDONLY(23)%PTR%GET_DEVICE_DATA_RDONLY(PNICE)
    CALL FIELD_STATE%TENDENCY_TMP%F_T%GET_DEVICE_DATA_RDONLY(TEND_TMP_T)
    CALL FIELD_STATE%TENDENCY_TMP%F_Q%GET_DEVICE_DATA_RDONLY(TEND_TMP_Q)
    CALL FIELD_STATE%TENDENCY_TMP%F_A%GET_DEVICE_DATA_RDONLY(TEND_TMP_A)
    CALL FIELD_STATE%TENDENCY_TMP%F_CLD%GET_DEVICE_DATA_RDONLY(TEND_TMP_CLD)

    CALL FIELD_STATE%F_PLUDE%GET_DEVICE_DATA_RDWR(PLUDE)
    CALL FIELD_STATE%F_PCOVPTOT%GET_DEVICE_DATA_RDWR(PCOVPTOT)
    CALL FIELD_STATE%F_PRAINFRAC_TOPRFZ%GET_DEVICE_DATA_RDWR(PRAINFRAC_TOPRFZ)
    CALL FIELD_STATE%FIELDS_RWONLY(1)%PTR%GET_DEVICE_DATA_RDWR(PFSQLF)
    CALL FIELD_STATE%FIELDS_RWONLY(2)%PTR%GET_DEVICE_DATA_RDWR(PFSQIF)
    CALL FIELD_STATE%FIELDS_RWONLY(3)%PTR%GET_DEVICE_DATA_RDWR(PFCQLNG)
    CALL FIELD_STATE%FIELDS_RWONLY(4)%PTR%GET_DEVICE_DATA_RDWR(PFCQNNG)
    CALL FIELD_STATE%FIELDS_RWONLY(5)%PTR%GET_DEVICE_DATA_RDWR(PFSQRF)
    CALL FIELD_STATE%FIELDS_RWONLY(6)%PTR%GET_DEVICE_DATA_RDWR(PFSQSF)
    CALL FIELD_STATE%FIELDS_RWONLY(7)%PTR%GET_DEVICE_DATA_RDWR(PFCQRNG)
    CALL FIELD_STATE%FIELDS_RWONLY(8)%PTR%GET_DEVICE_DATA_RDWR(PFCQSNG)
    CALL FIELD_STATE%FIELDS_RWONLY(9)%PTR%GET_DEVICE_DATA_RDWR(PFSQLTUR)
    CALL FIELD_STATE%FIELDS_RWONLY(10)%PTR%GET_DEVICE_DATA_RDWR(PFSQITUR)
    CALL FIELD_STATE%FIELDS_RWONLY(11)%PTR%GET_DEVICE_DATA_RDWR(PFPLSL)
    CALL FIELD_STATE%FIELDS_RWONLY(12)%PTR%GET_DEVICE_DATA_RDWR(PFPLSN)
    CALL FIELD_STATE%FIELDS_RWONLY(13)%PTR%GET_DEVICE_DATA_RDWR(PFHPSL)
    CALL FIELD_STATE%FIELDS_RWONLY(14)%PTR%GET_DEVICE_DATA_RDWR(PFHPSN)
    CALL FIELD_STATE%TENDENCY_LOC%F_T%GET_DEVICE_DATA_RDWR(TEND_LOC_T)
    CALL FIELD_STATE%TENDENCY_LOC%F_Q%GET_DEVICE_DATA_RDWR(TEND_LOC_Q)
    CALL FIELD_STATE%TENDENCY_LOC%F_A%GET_DEVICE_DATA_RDWR(TEND_LOC_A)
    CALL FIELD_STATE%TENDENCY_LOC%F_CLD%GET_DEVICE_DATA_RDWR(TEND_LOC_CLD)

!$acc data copyin(yrecldp) deviceptr(PT, PQ,TEND_TMP_T,TEND_TMP_Q,&
!$acc & TEND_TMP_A, TEND_TMP_CLD, TEND_LOC_T, TEND_LOC_Q, &
!$acc & TEND_LOC_A, TEND_LOC_CLD, PVFA, PVFL, PVFI, &
!$acc & PDYNA, PDYNL, PDYNI, PHRSW,    PHRLW,&
!$acc & PVERVEL,  PAP,      PAPH,&
!$acc & PLSM,       LDCUM,      KTYPE, &
!$acc & PLU,      PLUDE,    PSNDE,    PMFU,     PMFD,&
!$acc & PA,       PCLV,   PSUPSAT,&
!$acc & PLCRIT_AER,PICRIT_AER,&
!$acc & PRE_ICE, PCCN,     PNICE,&
!$acc & PCOVPTOT, PRAINFRAC_TOPRFZ,&
!$acc & PFSQLF,   PFSQIF ,  PFCQNNG,  PFCQLNG,&
!$acc & PFSQRF,   PFSQSF ,  PFCQRNG,  PFCQSNG,&
!$acc & PFSQLTUR, PFSQITUR , &
!$acc & PFPLSL,   PFPLSN,   PFHPSL,   PFHPSN)

    ! Local timer for each thread
    TID = GET_THREAD_NUM()
    CALL TIMER%THREAD_START(TID)

!$acc parallel loop gang vector_length(NPROMA)
    DO JKGLO=1,NGPTOT,NPROMA
       IBL=(JKGLO-1)/NPROMA+1
       ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)

       CALL CLOUDSC_SCC &
        & (1, ICEND, NPROMA, NLEV, PTSPHY,&
        & PT(:,:,IBL), PQ(:,:,IBL), &
        & TEND_TMP_T(:,:,IBL), TEND_TMP_Q(:,:,IBL), TEND_TMP_A(:,:,IBL), TEND_TMP_CLD(:,:,:,IBL), &
        & TEND_LOC_T(:,:,IBL), TEND_LOC_Q(:,:,IBL), TEND_LOC_A(:,:,IBL), TEND_LOC_CLD(:,:,:,IBL), &
        & PVFA(:,:,IBL), PVFL(:,:,IBL), PVFI(:,:,IBL), PDYNA(:,:,IBL), PDYNL(:,:,IBL), PDYNI(:,:,IBL), &
        & PHRSW(:,:,IBL),    PHRLW(:,:,IBL),&
        & PVERVEL(:,:,IBL),  PAP(:,:,IBL),      PAPH(:,:,IBL),&
        & PLSM(:,IBL),       LDCUM(:,IBL),      KTYPE(:,IBL), &
        & PLU(:,:,IBL),      PLUDE(:,:,IBL),    PSNDE(:,:,IBL),    PMFU(:,:,IBL),     PMFD(:,:,IBL),&
        !---prognostic fields
        & PA(:,:,IBL),       PCLV(:,:,:,IBL),   PSUPSAT(:,:,IBL),&
        !-- arrays for aerosol-cloud interactions
        & PLCRIT_AER(:,:,IBL),PICRIT_AER(:,:,IBL),&
        & PRE_ICE(:,:,IBL),&
        & PCCN(:,:,IBL),     PNICE(:,:,IBL),&
        !---diagnostic output
        & PCOVPTOT(:,:,IBL), PRAINFRAC_TOPRFZ(:,IBL),&
        !---resulting fluxes
        & PFSQLF(:,:,IBL),   PFSQIF (:,:,IBL),  PFCQNNG(:,:,IBL),  PFCQLNG(:,:,IBL),&
        & PFSQRF(:,:,IBL),   PFSQSF (:,:,IBL),  PFCQRNG(:,:,IBL),  PFCQSNG(:,:,IBL),&
        & PFSQLTUR(:,:,IBL), PFSQITUR (:,:,IBL), &
        & PFPLSL(:,:,IBL),   PFPLSN(:,:,IBL),   PFHPSL(:,:,IBL),   PFHPSN(:,:,IBL),&
        & YRECLDP=YRECLDP)

    ENDDO
!$acc end parallel loop
!$acc end data
    
    CALL TIMER%THREAD_END(TID)

    IF(USE_PACKED)THEN
       CALL FIELD_STATE%DATA_RWONLY%SYNC_HOST_RDWR()
       ! If this is called then the subsequent FIELD_STATE%FIELDS_RWONLY%PTR%SYNC_HOST_RDWR() calls
       ! don't trigger any data movement
    ENDIF

    CALL FIELD_STATE%F_PLUDE%SYNC_HOST_RDWR()
    CALL FIELD_STATE%F_PCOVPTOT%SYNC_HOST_RDWR()
    CALL FIELD_STATE%F_PRAINFRAC_TOPRFZ%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(1)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(2)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(3)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(4)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(5)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(6)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(7)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(8)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(9)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(10)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(11)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(12)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(13)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%FIELDS_RWONLY(14)%PTR%SYNC_HOST_RDWR()
    CALL FIELD_STATE%TENDENCY_LOC%F_T%SYNC_HOST_RDWR()
    CALL FIELD_STATE%TENDENCY_LOC%F_Q%SYNC_HOST_RDWR()
    CALL FIELD_STATE%TENDENCY_LOC%F_A%SYNC_HOST_RDWR()
    CALL FIELD_STATE%TENDENCY_LOC%F_CLD%SYNC_HOST_RDWR()

    CALL TIMER%END()

    ! On GPUs, adding block-level column totals is cumbersome and
    ! error prone, and of little value due to the large number of
    ! processing "thread teams". Instead we register the total here.
    CALL TIMER%THREAD_LOG(TID=TID, IGPC=NGPTOT)

    CALL TIMER%PRINT_PERFORMANCE(NPROMA, NGPBLKS, NGPTOT)

  END SUBROUTINE CLOUDSC_DRIVER_GPU_SCC_FIELD

END MODULE CLOUDSC_DRIVER_GPU_SCC_FIELD_MOD
