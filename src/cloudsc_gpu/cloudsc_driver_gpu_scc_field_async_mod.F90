! (C) Copyright 1988- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE CLOUDSC_DRIVER_GPU_SCC_FIELD_ASYNC_MOD

  USE PARKIND1, ONLY: JPIM, JPRB
  USE YOMPHYDER, ONLY: STATE_TYPE
  USE YOECLDP, ONLY : NCLV, YRECLDP, TECLDP
  USE CLOUDSC_MPI_MOD, ONLY: NUMPROC, IRANK
  USE TIMER_MOD, ONLY : PERFORMANCE_TIMER, GET_THREAD_NUM

  USE CLOUDSC_GPU_SCC_MOD, ONLY: CLOUDSC_SCC
  USE CLOUDSC_AUX_TYPE_MOD, ONLY: CLOUDSC_AUX_TYPE
  USE CLOUDSC_FLUX_TYPE_MOD, ONLY: CLOUDSC_FLUX_TYPE
  USE CLOUDSC_STATE_TYPE_MOD, ONLY: CLOUDSC_STATE_TYPE
  USE FIELD_MODULE, ONLY: GET_DEVICE_DATA, ENSURE_HOST
  USE FIELD_DEFAULTS_MODULE, ONLY: INIT_QUEUES, DELETE_QUEUES

  IMPLICIT NONE

CONTAINS

  SUBROUTINE CLOUDSC_DRIVER_GPU_SCC_FIELD_ASYNC( &
     & NUMOMP, NPROMA, NLEV, NGPTOT, NGPBLKS, NGPTOTG, KFLDX, PTSPHY, &
     & AUX, FLUX, TENDENCY_TMP, TENDENCY_LOC, NQUEUES)
     
    ! Driver routine that invokes the optimized CLAW-based CLOUDSC GPU kernel

    INTEGER(KIND=JPIM)                                    :: NUMOMP, NPROMA, NLEV, NGPTOT, NGPBLKS, NGPTOTG
    INTEGER(KIND=JPIM)                                    :: KFLDX 
    REAL(KIND=JPRB)                                       :: PTSPHY       ! Physics timestep
    TYPE(CLOUDSC_AUX_TYPE),   INTENT(INOUT)               :: AUX
    TYPE(CLOUDSC_FLUX_TYPE),  INTENT(INOUT)               :: FLUX
    TYPE(CLOUDSC_STATE_TYPE), INTENT(INOUT)               :: TENDENCY_LOC, TENDENCY_TMP
    INTEGER(KIND=JPIM), INTENT(IN)                        :: NQUEUES

    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PT(:,:,:)       ! T at start of callpar
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PQ(:,:,:)       ! Q at start of callpar
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PVFA(:,:,:)     ! CC from VDF scheme
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PVFL(:,:,:)     ! Liq from VDF scheme
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PVFI(:,:,:)     ! Ice from VDF scheme
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PDYNA(:,:,:)    ! CC from Dynamics
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PDYNL(:,:,:)    ! Liq from Dynamics
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PDYNI(:,:,:)    ! Liq from Dynamics
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PHRSW(:,:,:)    ! Short-wave heating rate
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PHRLW(:,:,:)    ! Long-wave heating rate
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PVERVEL(:,:,:)  ! Vertical velocity
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PAP(:,:,:)      ! Pressure on full levels
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PAPH(:,:,:)     ! Pressure on half levels
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLSM(:,:)       ! Land fraction (0-1)
    LOGICAL, POINTER, CONTIGUOUS          :: LDCUM(:,:)      ! Convection active
    INTEGER(KIND=JPIM),POINTER, CONTIGUOUS:: KTYPE(:,:)      ! Convection type 0,1,2
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLU(:,:,:)      ! Conv. condensate
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLUDE(:,:,:)    ! Conv. detrained water
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PSNDE(:,:,:)    ! Conv. detrained snow
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PMFU(:,:,:)     ! Conv. mass flux up
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PMFD(:,:,:)     ! Conv. mass flux down
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PA(:,:,:)       ! Original Cloud fraction (t)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PCLV(:,:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PSUPSAT(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLCRIT_AER(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PICRIT_AER(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PRE_ICE(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PCCN(:,:,:)     ! liquid cloud condensation nuclei
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PNICE(:,:,:)    ! ice number concentration (cf. CCN)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PCOVPTOT(:,:,:)    ! Precip fraction
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PRAINFRAC_TOPRFZ(:,:)
    ! Flux diagnostics for DDH budget
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQLF(:,:,:)    ! Flux of liquid
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQIF(:,:,:)    ! Flux of ice
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFCQLNG(:,:,:)   ! -ve corr for liq
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFCQNNG(:,:,:)   ! -ve corr for ice
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQRF(:,:,:)    ! Flux diagnostics
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQSF(:,:,:)    !    for DDH, generic
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFCQRNG(:,:,:)   ! rain
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFCQSNG(:,:,:)   ! snow
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQLTUR(:,:,:)  ! liquid flux due to VDF
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFSQITUR(:,:,:)  ! ice flux due to VDF
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFPLSL(:,:,:)    ! liq+rain sedim flux
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFPLSN(:,:,:)    ! ice+snow sedim flux
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFHPSL(:,:,:)    ! Enthalpy flux for liq
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PFHPSN(:,:,:)    ! ice number concentration (cf. CCN)

    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_T(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_Q(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_A(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_CLD(:,:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_T(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_Q(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_A(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_CLD(:,:,:,:)

    INTEGER(KIND=JPIM) :: JKGLO,IBL,ICEND
    TYPE(PERFORMANCE_TIMER) :: TIMER
    INTEGER(KIND=JPIM) :: TID ! thread id from 0 .. NUMOMP - 1

     ! double blocking variables
    INTEGER(KIND=JPIM) :: QUEUE     ! block size for blocks in outer loop
    INTEGER(KIND=JPIM) :: BLOCK_BUFFER_SIZE     ! block size for blocks in outer loop
    INTEGER(KIND=JPIM) :: BLOCK_COUNT           ! number of blocks
    INTEGER(KIND=JPIM) :: BLOCK_IDX             ! idx of current block in [1,BLOCK_COUNT]
    INTEGER(KIND=JPIM) :: BLOCK_START           ! start of current block in [1,NGPBLKS]
    INTEGER(KIND=JPIM) :: BLOCK_SIZE             ! end of current block in [1,NGPBLKS]
    INTEGER(KIND=JPIM) :: IBLLOC                ! local loop idx inside inner block loop
    INTEGER(KIND=JPIM) :: BLK_BOUNDS(2)         ! Array holding BLOCK_START and BLOCK_SIZE

    NGPBLKS = (NGPTOT / NPROMA) + MIN(MOD(NGPTOT,NPROMA), 1)
1003 format(5x,'NUMPROC=',i0,', NUMOMP=',i0,', NGPTOTG=',i0,', NPROMA=',i0,', NGPBLKS=',i0)
    if (irank == 0) then
      write(0,1003) NUMPROC,NUMOMP,NGPTOTG,NPROMA,NGPBLKS
    end if

    ! Global timer for the parallel region
    CALL TIMER%START(NUMOMP)

    print *, 'NQUEUES=', NQUEUES
    
    ! Set F-API to use async blocked copies for transfers
    CALL INIT_QUEUES(NQUEUES)

    CALL AUX%F_PT%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PQ%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PVFA%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PVFL%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PVFI%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PDYNA%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PDYNL%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PDYNI%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PHRSW%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PHRLW%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PVERVEL%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PAP%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PLU%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PSNDE%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PMFU%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PMFD%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PA%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PSUPSAT%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PLCRIT_AER%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PICRIT_AER%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PRE_ICE%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PCCN%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PNICE%SYNC_DEVICE_RDONLY()
    
    CALL AUX%F_PAPH%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PLSM%SYNC_DEVICE_RDONLY()
    CALL AUX%F_LDCUM%SYNC_DEVICE_RDONLY()
    CALL AUX%F_KTYPE%SYNC_DEVICE_RDONLY()
    CALL AUX%F_PCLV%SYNC_DEVICE_RDONLY()
    
    CALL TENDENCY_TMP%F_T%SYNC_DEVICE_RDONLY()
    CALL TENDENCY_TMP%F_Q%SYNC_DEVICE_RDONLY()
    CALL TENDENCY_TMP%F_A%SYNC_DEVICE_RDONLY()
    CALL TENDENCY_TMP%F_CLD%SYNC_DEVICE_RDONLY()

    CALL AUX%F_PLUDE%SYNC_DEVICE_RDWR()
    CALL AUX%F_PCOVPTOT%SYNC_DEVICE_RDWR()
    CALL AUX%F_PRAINFRAC_TOPRFZ%SYNC_DEVICE_RDWR()
    
    CALL FLUX%F_PFSQLF%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFSQIF%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFCQLNG%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFCQNNG%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFSQRF%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFSQSF%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFCQRNG%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFCQSNG%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFSQLTUR%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFSQITUR%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFPLSL%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFPLSN%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFHPSL%SYNC_DEVICE_RDWR()
    CALL FLUX%F_PFHPSN%SYNC_DEVICE_RDWR()
    
    CALL TENDENCY_LOC%F_T%SYNC_DEVICE_RDWR()
    CALL TENDENCY_LOC%F_Q%SYNC_DEVICE_RDWR()
    CALL TENDENCY_LOC%F_A%SYNC_DEVICE_RDWR()
    CALL TENDENCY_LOC%F_CLD%SYNC_DEVICE_RDWR()


    
    DO QUEUE=1,NQUEUES
      CALL AUX%F_PT%GET_DEVICE_DATA_RDONLY(PT, QUEUE=QUEUE)
      CALL AUX%F_PQ%GET_DEVICE_DATA_RDONLY(PQ, QUEUE=QUEUE)
      CALL AUX%F_PVFA%GET_DEVICE_DATA_RDONLY(PVFA, QUEUE=QUEUE)
      CALL AUX%F_PVFL%GET_DEVICE_DATA_RDONLY(PVFL, QUEUE=QUEUE)
      CALL AUX%F_PVFI%GET_DEVICE_DATA_RDONLY(PVFI, QUEUE=QUEUE)
      CALL AUX%F_PDYNA%GET_DEVICE_DATA_RDONLY(PDYNA, QUEUE=QUEUE)
      CALL AUX%F_PDYNL%GET_DEVICE_DATA_RDONLY(PDYNL, QUEUE=QUEUE)
      CALL AUX%F_PDYNI%GET_DEVICE_DATA_RDONLY(PDYNI, QUEUE=QUEUE)
      CALL AUX%F_PHRSW%GET_DEVICE_DATA_RDONLY(PHRSW, QUEUE=QUEUE)
      CALL AUX%F_PHRLW%GET_DEVICE_DATA_RDONLY(PHRLW, QUEUE=QUEUE)
      CALL AUX%F_PVERVEL%GET_DEVICE_DATA_RDONLY(PVERVEL, QUEUE=QUEUE)
      CALL AUX%F_PAP%GET_DEVICE_DATA_RDONLY(PAP, QUEUE=QUEUE)
      CALL AUX%F_PLU%GET_DEVICE_DATA_RDONLY(PLU, QUEUE=QUEUE)
      CALL AUX%F_PSNDE%GET_DEVICE_DATA_RDONLY(PSNDE, QUEUE=QUEUE)
      CALL AUX%F_PMFU%GET_DEVICE_DATA_RDONLY(PMFU, QUEUE=QUEUE)
      CALL AUX%F_PMFD%GET_DEVICE_DATA_RDONLY(PMFD, QUEUE=QUEUE)
      CALL AUX%F_PA%GET_DEVICE_DATA_RDONLY(PA, QUEUE=QUEUE)
      CALL AUX%F_PSUPSAT%GET_DEVICE_DATA_RDONLY(PSUPSAT, QUEUE=QUEUE)
      CALL AUX%F_PLCRIT_AER%GET_DEVICE_DATA_RDONLY(PLCRIT_AER, QUEUE=QUEUE)
      CALL AUX%F_PICRIT_AER%GET_DEVICE_DATA_RDONLY(PICRIT_AER, QUEUE=QUEUE)
      CALL AUX%F_PRE_ICE%GET_DEVICE_DATA_RDONLY(PRE_ICE, QUEUE=QUEUE)
      CALL AUX%F_PCCN%GET_DEVICE_DATA_RDONLY(PCCN, QUEUE=QUEUE)
      CALL AUX%F_PNICE%GET_DEVICE_DATA_RDONLY(PNICE, QUEUE=QUEUE)
      
      CALL AUX%F_PAPH%GET_DEVICE_DATA_RDONLY(PAPH, QUEUE=QUEUE)
      CALL AUX%F_PLSM%GET_DEVICE_DATA_RDONLY(PLSM, QUEUE=QUEUE)
      CALL AUX%F_LDCUM%GET_DEVICE_DATA_RDONLY(LDCUM, QUEUE=QUEUE)
      CALL AUX%F_KTYPE%GET_DEVICE_DATA_RDONLY(KTYPE, QUEUE=QUEUE)
      CALL AUX%F_PCLV%GET_DEVICE_DATA_RDONLY(PCLV, QUEUE=QUEUE)
      
      CALL TENDENCY_TMP%F_T%GET_DEVICE_DATA_RDONLY(TEND_TMP_T, QUEUE=QUEUE)
      CALL TENDENCY_TMP%F_Q%GET_DEVICE_DATA_RDONLY(TEND_TMP_Q, QUEUE=QUEUE)
      CALL TENDENCY_TMP%F_A%GET_DEVICE_DATA_RDONLY(TEND_TMP_A, QUEUE=QUEUE)
      CALL TENDENCY_TMP%F_CLD%GET_DEVICE_DATA_RDONLY(TEND_TMP_CLD, QUEUE=QUEUE)

      CALL AUX%F_PLUDE%GET_DEVICE_DATA_RDWR(PLUDE, QUEUE=QUEUE)
      CALL AUX%F_PCOVPTOT%GET_DEVICE_DATA_RDWR(PCOVPTOT, QUEUE=QUEUE)
      CALL AUX%F_PRAINFRAC_TOPRFZ%GET_DEVICE_DATA_RDWR(PRAINFRAC_TOPRFZ, QUEUE=QUEUE)
      
      CALL FLUX%F_PFSQLF%GET_DEVICE_DATA_RDWR(PFSQLF, QUEUE=QUEUE)
      CALL FLUX%F_PFSQIF%GET_DEVICE_DATA_RDWR(PFSQIF, QUEUE=QUEUE)
      CALL FLUX%F_PFCQLNG%GET_DEVICE_DATA_RDWR(PFCQLNG, QUEUE=QUEUE)
      CALL FLUX%F_PFCQNNG%GET_DEVICE_DATA_RDWR(PFCQNNG, QUEUE=QUEUE)
      CALL FLUX%F_PFSQRF%GET_DEVICE_DATA_RDWR(PFSQRF, QUEUE=QUEUE)
      CALL FLUX%F_PFSQSF%GET_DEVICE_DATA_RDWR(PFSQSF, QUEUE=QUEUE)
      CALL FLUX%F_PFCQRNG%GET_DEVICE_DATA_RDWR(PFCQRNG, QUEUE=QUEUE)
      CALL FLUX%F_PFCQSNG%GET_DEVICE_DATA_RDWR(PFCQSNG, QUEUE=QUEUE)
      CALL FLUX%F_PFSQLTUR%GET_DEVICE_DATA_RDWR(PFSQLTUR, QUEUE=QUEUE)
      CALL FLUX%F_PFSQITUR%GET_DEVICE_DATA_RDWR(PFSQITUR, QUEUE=QUEUE)
      CALL FLUX%F_PFPLSL%GET_DEVICE_DATA_RDWR(PFPLSL, QUEUE=QUEUE)
      CALL FLUX%F_PFPLSN%GET_DEVICE_DATA_RDWR(PFPLSN, QUEUE=QUEUE)
      CALL FLUX%F_PFHPSL%GET_DEVICE_DATA_RDWR(PFHPSL, QUEUE=QUEUE)
      CALL FLUX%F_PFHPSN%GET_DEVICE_DATA_RDWR(PFHPSN, QUEUE=QUEUE)
      
      CALL TENDENCY_LOC%F_T%GET_DEVICE_DATA_RDWR(TEND_LOC_T, QUEUE=QUEUE)
      CALL TENDENCY_LOC%F_Q%GET_DEVICE_DATA_RDWR(TEND_LOC_Q, QUEUE=QUEUE)
      CALL TENDENCY_LOC%F_A%GET_DEVICE_DATA_RDWR(TEND_LOC_A, QUEUE=QUEUE)
      CALL TENDENCY_LOC%F_CLD%GET_DEVICE_DATA_RDWR(TEND_LOC_CLD, QUEUE=QUEUE)

!$acc data copyin(yrecldp) &
#ifndef FIELD_API_DISABLE_MAPPED_MEMORY 
!$acc & present( &
#else
!$acc & deviceptr( &
#endif
!$acc & PT, PQ,TEND_TMP_T,TEND_TMP_Q,&
!$acc & TEND_TMP_A, TEND_TMP_CLD, TEND_LOC_T, TEND_LOC_Q, &
!$acc & TEND_LOC_A, TEND_LOC_CLD, PVFA, PVFL, PVFI, &
!$acc & PDYNA, PDYNL, PDYNI, PHRSW,    PHRLW,&
!$acc & PVERVEL,  PAP,      PAPH,&
!$acc & PLSM,       LDCUM,      KTYPE, &
!$acc & PLU,      PLUDE,    PSNDE,    PMFU,     PMFD,&
!$acc & PA,       PCLV,   PSUPSAT,&
!$acc & PLCRIT_AER,PICRIT_AER,&
!$acc & PRE_ICE, PCCN,     PNICE,&
!$acc & PCOVPTOT, PRAINFRAC_TOPRFZ,&
!$acc & PFSQLF,   PFSQIF ,  PFCQNNG,  PFCQLNG,&
!$acc & PFSQRF,   PFSQSF ,  PFCQRNG,  PFCQSNG,&
!$acc & PFSQLTUR, PFSQITUR , &
!$acc & PFPLSL,   PFPLSN,   PFHPSL,   PFHPSN) &
!$acc & async(QUEUE)

      ! Local timer for each thread
      TID = GET_THREAD_NUM()
      CALL TIMER%THREAD_START(TID)
      
      BLOCK_BUFFER_SIZE = (NGPBLKS-1)/NQUEUES + 1
      IF (QUEUE < NQUEUES) THEN
        BLOCK_SIZE = BLOCK_BUFFER_SIZE
      ELSE
        BLOCK_SIZE =  NGPBLKS+BLOCK_BUFFER_SIZE -  NQUEUES*BLOCK_BUFFER_SIZE
      END IF
      
!$acc parallel loop gang vector_length(NPROMA) async(QUEUE)
      DO IBLLOC=1, BLOCK_SIZE
        IBL= BLOCK_BUFFER_SIZE*(QUEUE-1)+IBLLOC
        JKGLO=(IBL-1)*NPROMA+1
        ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      ! DO JKGLO=1,NGPTOT,NPROMA ! just a way to loop over NGPBLKS
      !    IBL=(JKGLO-1)/NPROMA+1
      !    ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)

         CALL CLOUDSC_SCC &
          & (1, ICEND, NPROMA, NLEV, PTSPHY,&
          & PT(:,:,IBL), PQ(:,:,IBL), &
          & TEND_TMP_T(:,:,IBL), TEND_TMP_Q(:,:,IBL), TEND_TMP_A(:,:,IBL), TEND_TMP_CLD(:,:,:,IBL), &
          & TEND_LOC_T(:,:,IBL), TEND_LOC_Q(:,:,IBL), TEND_LOC_A(:,:,IBL), TEND_LOC_CLD(:,:,:,IBL), &
          & PVFA(:,:,IBL), PVFL(:,:,IBL), PVFI(:,:,IBL), PDYNA(:,:,IBL), PDYNL(:,:,IBL), PDYNI(:,:,IBL), &
          & PHRSW(:,:,IBL),    PHRLW(:,:,IBL),&
          & PVERVEL(:,:,IBL),  PAP(:,:,IBL),      PAPH(:,:,IBL),&
          & PLSM(:,IBL),       LDCUM(:,IBL),      KTYPE(:,IBL), &
          & PLU(:,:,IBL),      PLUDE(:,:,IBL),    PSNDE(:,:,IBL),    PMFU(:,:,IBL),     PMFD(:,:,IBL),&
          !---prognostic fields
          & PA(:,:,IBL),       PCLV(:,:,:,IBL),   PSUPSAT(:,:,IBL),&
          !-- arrays for aerosol-cloud interactions
          & PLCRIT_AER(:,:,IBL),PICRIT_AER(:,:,IBL),&
          & PRE_ICE(:,:,IBL),&
          & PCCN(:,:,IBL),     PNICE(:,:,IBL),&
          !---diagnostic output
          & PCOVPTOT(:,:,IBL), PRAINFRAC_TOPRFZ(:,IBL),&
          !---resulting fluxes
          & PFSQLF(:,:,IBL),   PFSQIF (:,:,IBL),  PFCQNNG(:,:,IBL),  PFCQLNG(:,:,IBL),&
          & PFSQRF(:,:,IBL),   PFSQSF (:,:,IBL),  PFCQRNG(:,:,IBL),  PFCQSNG(:,:,IBL),&
          & PFSQLTUR(:,:,IBL), PFSQITUR (:,:,IBL), &
          & PFPLSL(:,:,IBL),   PFPLSN(:,:,IBL),   PFHPSL(:,:,IBL),   PFHPSN(:,:,IBL),&
          & YRECLDP=YRECLDP)

      ENDDO
!$acc end parallel loop
!$acc end data

      CALL TIMER%THREAD_END(TID)

    END DO ! End of block loop

     CALL AUX%F_PLUDE%SYNC_HOST_RDWR()
     CALL AUX%F_PCOVPTOT%SYNC_HOST_RDWR()
     CALL AUX%F_PRAINFRAC_TOPRFZ%SYNC_HOST_RDWR()
     CALL FLUX%F_PFSQLF%SYNC_HOST_RDWR()
     CALL FLUX%F_PFSQIF%SYNC_HOST_RDWR()
     CALL FLUX%F_PFCQLNG%SYNC_HOST_RDWR()
     CALL FLUX%F_PFCQNNG%SYNC_HOST_RDWR()
     CALL FLUX%F_PFSQRF%SYNC_HOST_RDWR()
     CALL FLUX%F_PFSQSF%SYNC_HOST_RDWR()
     CALL FLUX%F_PFCQRNG%SYNC_HOST_RDWR()
     CALL FLUX%F_PFCQSNG%SYNC_HOST_RDWR()
     CALL FLUX%F_PFSQLTUR%SYNC_HOST_RDWR()
     CALL FLUX%F_PFSQITUR%SYNC_HOST_RDWR()
     CALL FLUX%F_PFPLSL%SYNC_HOST_RDWR()
     CALL FLUX%F_PFPLSN%SYNC_HOST_RDWR()
     CALL FLUX%F_PFHPSL%SYNC_HOST_RDWR()
     CALL FLUX%F_PFHPSN%SYNC_HOST_RDWR()
     CALL TENDENCY_LOC%F_T%SYNC_HOST_RDWR()
     CALL TENDENCY_LOC%F_Q%SYNC_HOST_RDWR()
     CALL TENDENCY_LOC%F_A%SYNC_HOST_RDWR()
     CALL TENDENCY_LOC%F_CLD%SYNC_HOST_RDWR()

    !$acc wait ! Wait for all async queues to finish

    CALL DELETE_QUEUES()

    CALL TIMER%END()

    ! On GPUs, adding block-level column totals is cumbersome and
    ! error prone, and of little value due to the large number of
    ! processing "thread teams". Instead we register the total here.
    CALL TIMER%THREAD_LOG(TID=TID, IGPC=NGPTOT)

    CALL TIMER%PRINT_PERFORMANCE(NPROMA, NGPBLKS, NGPTOT)

  END SUBROUTINE CLOUDSC_DRIVER_GPU_SCC_FIELD_ASYNC

END MODULE CLOUDSC_DRIVER_GPU_SCC_FIELD_ASYNC_MOD
